#!/usr/bin/coffee
'use strict'


{argv, define, one, pinboard, respond, verify} = (require './tools')

verify()

command = (define 'Returns the most recent time a bookmark was added, updated or deleted. Use this before calling `all` to see if the data has changed since the last fetch.')

command
  .option '-f --format [fmt]', 'output format', /^(color|json)$/i, 'color'
  .option '-v --verbose', 'display all output'

  .action ->
    format = one(command.format, process.env.PINBOARD_FORMAT)

    try
      pinboard().update (result) ->
        return if one(command.verbose, process.env.PINBOARD_VERBOSE) is no

        if format is 'color'
          time = (require 'moment')(result.update_time).fromNow()
          result.update_time = "Last updated #{time}."

        respond result, format, 'update_time'

    catch e
      respond e, format, 'error'

  .parse argv()
