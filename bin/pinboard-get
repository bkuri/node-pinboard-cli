#!/usr/bin/coffee
'use strict'

{ PINBOARD_FORMAT } = process.env
{ argv, define, one, pinboard, yesno } = require('../lib/tools')
get = define('Returns one or more posts on a single day matching the arguments. If no date or url is given, date of most recent bookmark will be used.')

get
  .option '-d --date <date>', 'filter by date'
  .option '-f --format <fmt>', 'output format', /^(color|json)$/i
  .option '-m --meta', 'include a change detection signature in meta'
  .option '-t --tags <tags>', 'filter by < 4 tags (comma-separated)'
  .option '-u --url <url>', 'get a specific url'

  .action ->
    { format } = require('../lib/format')
    respond = format one(get.format, PINBOARD_FORMAT)

    callback = (result) ->
      respond result, 'bookmarks'
      return

    try
      options =
        dt: get.date
        meta: yesno(get.meta)
        tags: get.tags
        url: get.url

      pinboard().get options, callback
      return

    catch error
      respond error, 'error'
      return

  .parse argv(get.options)
