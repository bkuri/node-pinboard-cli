#!/usr/bin/coffee
'use strict'

{ PINBOARD_FORMAT } = process.env
{ argv, define, one, pinboard } = require('../lib/tools')

dates = define('Returns a list of dates with the number of posts at each date.')

dates
  .option '-f --format <fmt>', 'output format', /^(color|json)$/i
  .option '-t --tags <tags>', 'filter by up to 3 tags (comma-separated)'

  .action ->
    { format } = require('../lib/format')
    respond = format one(dates.format, PINBOARD_FORMAT)

    callback = (result) ->
      respond result, 'dates'
      return

    try
      pinboard().dates { tags: dates.tags }, callback
      return

    catch error
      respond error, 'error'
      return

  .parse argv(dates.options)
