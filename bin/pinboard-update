#!/usr/bin/coffee
'use strict'


{PINBOARD_FORMAT, PINBOARD_VERBOSE} = process.env
{argv, define, one, pinboard} = require('../lib/tools')


update = define('Returns the most recent time a bookmark was added, updated or deleted. Use this before calling `all` to see if the data has changed since the last fetch.')

update
  .option '-f --format [fmt]', 'output format', /^(color|json)$/i, PINBOARD_FORMAT
  .option '-v --verbose', 'display all output'

  .action ->
    {format} = require('../lib/format')
    style = one(update.format, PINBOARD_FORMAT)
    respond = format(style)

    try
      pinboard().update (result) ->
        return if one(update.verbose, PINBOARD_VERBOSE) is no

        if style is 'color'
          time = require('moment')(result.update_time).fromNow()
          result.update_time = "Last updated #{time}."

        respond result, 'update_time'

    catch error
      respond error, 'error'

  .parse argv()
