#!/usr/bin/coffee
'use strict'


{PINBOARD_FORMAT} = process.env
{argv, define, format, one, pinboard} = require('./tools')


dates = define('Returns a list of dates with the number of posts at each date.')

dates
  .option '-f --format [fmt]', 'output format', /^(color|json)$/i, PINBOARD_FORMAT
  .option '-t --tags <tags>', 'filter by up to 3 tags (comma-separated)'

  .action ->
    respond = format one(dates.format, PINBOARD_FORMAT)

    try
      options = switch
        when dates.tags? then tags: dates.tags
        else {}

      pinboard().dates options, (result) ->
        respond result, 'date'

    catch error
      respond error, 'error'

  .parse argv()
