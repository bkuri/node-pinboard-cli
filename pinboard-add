#!/usr/bin/coffee
'use strict'


{PINBOARD_FORMAT, PINBOARD_VERBOSE} = process.env
{argv, define, format, one, pinboard, yesno} = require('./tools')


add = define('Add a bookmark.')

add
  .option '-d --date [date]', 'Date', require('moment')().format()
  .option '-e --extended [ext]', 'Extended description'
  .option '-f --format [fmt]', 'output format', /^(color|json)$/i, PINBOARD_FORMAT
  .option '-r --replace', 'Replace existing entry'
  .option '-R --to-read', 'Tag to read later'
  .option '-s --shared', 'Shared'
  .option '-t --tags <tags>', 'filter by up to 3 tags (comma-separated)'
  .option '-v --verbose', 'display all output'

  .action  (url, description) ->
    respond = format one(add.format, PINBOARD_FORMAT)

    try
      bookmark =
        description: description
        dt: add.date
        extended: add.extended
        replace: yesno(add.replaced)
        shared: yesno(add.shared)
        tags: add.tags
        toread: yesno(add.toRead)
        url: url

      pinboard().add bookmark, (result) ->
        return if one(add.verbose, PINBOARD_VERBOSE) is no
        respond result, 'result_code'

    catch error
      respond error, 'error'

  .usage '<url> <description>'
  .parse argv()
